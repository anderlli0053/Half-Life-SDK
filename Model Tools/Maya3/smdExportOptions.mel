global proc int smdExportOptions ( string $parent,
								   string $action,
								   string $initialSettings,
								   string $resultCallback )
//
//	Description:
//		This script posts the SMD file translator options.
//		The optionsString is of the form:
//			varName1=value1;varName2=value2;...
//
//	Parameters:
//		$parent - the elf parent layout for this options layout. It is
//					always a scrollLayout.
//		$action - the action that is to be performed with this invokation
//					of this proc. Valid options are:
//						"query" - construct the options string and pass it
//									to the resultCallback.
//						"post" - post all the elf controls.
//		$initialSettings - the current options string in effect at the
//						   time this script is invoked.
//		$resultCallback -
//				This is the proc to be called with the result string. 
//				resultCallback ( string $optionsString )
//
//	Returns:
//		1 if successfull.
//		0 otherwise.
//
{
	int		$bResult;
	string	$currentOptions;
	string	$optionList[];
	string	$optionBreakDown[];
	int		$index;

	if ($action == "post") {
		setParent $parent;

		columnLayout -adj true objTypeCol;
			radioButtonGrp
				-l "SMD File Type"
				-nrb 2 -cw3 175 100 100
				-la2 "Reference" "Animation" smdReference;
			radioButtonGrp
				-l "Rotate to 'Z Up' orientation"
				-nrb 2	-cw3 175 100 100
				-la2 "Yes" "No" smdYUp;                
			radioButtonGrp
				-l "Export all bones (even unskinned?)"
				-nrb 2	-cw3 175 100 100
				-la2 "Yes" "No" smdAllBones;                
                				
		// Now set to current settings.
		$currentOptions = $initialSettings;
		if (size($currentOptions) > 0) {
			tokenize($currentOptions, ";", $optionList);
			for ($index = 0; $index < size($optionList); $index++) {
				tokenize($optionList[$index], "=", $optionBreakDown);

				if ($optionBreakDown[0] == "reference") {
					if ($optionBreakDown[1] == "0") {
						radioButtonGrp -e -sl 2 smdReference;
					} else {
						radioButtonGrp -e -sl 1 smdReference;
					}
				} else if ($optionBreakDown[0] == "YUp") {
					if ($optionBreakDown[1] == "0") {
						radioButtonGrp -e -sl 2 smdYUp;
					} else {
						radioButtonGrp -e -sl 1 smdYUp;
					}
				} else if ($optionBreakDown[0] == "AllBones") {
					if ($optionBreakDown[1] == "0") {
						radioButtonGrp -e -sl 2 smdAllBones;
					} else {
						radioButtonGrp -e -sl 1 smdAllBones;
					}
				}
			}
		}
		$result = 1;
	
	} else if ($action == "query") {

		if (`radioButtonGrp -q -sl smdReference` == 1) {
			$currentOptions = $currentOptions + "reference=1";
		} else {
			$currentOptions = $currentOptions + "reference=0";
		}

		if (`radioButtonGrp -q -sl smdYUp` == 1) {
			$currentOptions = $currentOptions + ";YUp=1";
		} else {
			$currentOptions = $currentOptions + ";YUp=0";
		}

		if (`radioButtonGrp -q -sl smdAllBones` == 1) {
			$currentOptions = $currentOptions + ";AllBones=1";
		} else {
			$currentOptions = $currentOptions + ";AllBones=0";
		}

		eval($resultCallback+" \""+$currentOptions+"\"");
		$result = 1;
	} else {
		$bResult = 0;
	}
	
	return $bResult;
}
